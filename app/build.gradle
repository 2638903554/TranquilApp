apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "me.wcy.music"
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME

        manifestPlaceholders = [AMAP_KEY: getLocalValue("AMAP_KEY")]
    }

    signingConfigs {
        release {
            storeFile file("wangchenyan.keystore")
            storePassword getLocalValue("STORE_PASSWORD")
            keyAlias getLocalValue("KEY_ALIAS")
            keyPassword getLocalValue("KEY_PASSWORD")
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def getLocalValue(String key) {
    return getLocalValue(key, false)
}

def getLocalValue(String key, boolean quot) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def value = properties.containsKey(key) ? properties.get(key) : ""
    if (quot) {
        value = "\"" + value + "\""
    }
    return value
}

greendao {
    schemaVersion 1
    targetGenDir 'src/main/java'
    daoPackage "${android.defaultConfig.applicationId}.storage.db.greendao"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    implementation "com.android.support:design:$SUPPORT_VERSION"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.zhy:okhttputils:2.6.2'//网络访问（第三方封装）
    implementation 'com.google.code.gson:gson:2.8.2'//json文件转换
    implementation 'com.github.bumptech.glide:glide:3.8.0'//glide支持包 图片加载
    implementation("com.squareup.okhttp3:okhttp:4.0.0")//okhttp
    implementation 'me.wcy:lrcview:2.0.1'//歌词播放
    implementation 'com.hwangjr.rxbus:rxbus:2.0.0'//事件总线通讯库
    implementation 'org.greenrobot:greendao:3.2.2'//dao数据库接口
}
